PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  owl:  <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT ?class ?label ?desc ?prop ?propDesc ?oc ?otherClass ?superClass
WHERE {
    ?class rdf:type owl:Class ;
           rdfs:label ?label ;
           rdfs:comment ?desc .
    OPTIONAL {
    	?class rdfs:subClassOf ?sc .
    	OPTIONAL {
            ?sc owl:onProperty ?p .
            ?p rdfs:label ?prop ;
               rdfs:comment ?propDesc .
            { ?sc owl:allValuesFrom ?oc . }
            UNION
            { ?sc owl:someValuesFrom ?oc . }
            OPTIONAL {
        		?oc rdfs:label ?otherClass .
      		}
    	}
      	OPTIONAL {
      		?sc rdfs:label ?superClass .
    	}
  	}
}